#!/usr/bin/env joker

(ns wz-flow
  (:require [git :refer [repo current-branch]]
            [git-flow]
            [github]
            [joker.string :as str]
            [joker.os :as os]
            [joker.yaml :as yaml]))

(def config-file (str ((os/env) "HOME") "/.config/wize-flow"))
(defn- exit [msg] (println msg) (os/exit -1))

; load config
(when-not (os/exists? config-file)
  (let [user (do (print "github.com username: ") (read-line))
        pass (do (print "github.com password for" user "(never stored): ") (read-line)) ; todo: hide pass input
        otp  (do (print "two-factor authentication code: ") (read-line))
        [status resp] (github/personal-access-token user pass otp)]
    (if (= 201 status)
      (spit config-file (yaml/write-string {:github {:user user :token (resp "token")}}))
      (exit (str "GitHub " (resp "message") "\n" (resp "documentation_url"))))))

(def config (yaml/read-string (slurp config-file)))

; do some check on finish command
(when (some #(= "finish" %) *command-line-args*)
  (if-not (git-flow/working-branch? current-branch)
    (exit "The current HEAD is no feature branch.")
    
    (let [target-branch (git-flow/merge->branch current-branch)
          [status resp] (github/pull-requests (get-in config ["github" "token"]) (:owner repo) (:name repo) current-branch target-branch)
          pr (first resp)]
      (cond
        (not= 200 status)
        (exit (str "GitHub " (resp "message") "\n" (resp "documentation_url")))
        
        (empty? resp)
        (exit (format "No PR has been created from %v to %v on repository %v." current-branch target-branch (:name repo)))
        
        (nil? (pr "merged_at"))
        (exit (format "The PR #%v on repo %v/%v has not been merged yet." (pr "number") (:owner repo) (:name repo)))))))

(when (= "version" (first *command-line-args*))
  (println "git-flow" git-flow/version)
  (println "wize-flow 0.0.1")
  (os/exit -1))

; call git-flow command
(let [{:keys [out err]} (os/exec "git-flow" {:args *command-line-args*})]
  (print (-> (if (str/blank? out) err out)
             (str/replace "git flow" "git wize-flow"))))
